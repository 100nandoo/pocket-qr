plugins {
    id 'com.android.application'

    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'

    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}
apply from: '../android_commons.gradle'
import org.apache.tools.ant.taskdefs.condition.Os

def keystorePropertiesFile = file("../../signing/keystore.properties")
if (Os.isFamily(Os.FAMILY_WINDOWS)){
    keystorePropertiesFile = file("..\\..\\signing\\keystore.properties")
}
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    defaultConfig {
        applicationId Misc.appId

        resConfigs "en"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(Modules.preview)
    implementation project(Modules.core)

    implementation KotlinLibraries.kotlin
    implementation KotlinLibraries.coroutineGuava
    implementation KotlinLibraries.coroutinePlayServices

    /**
     * ==================== Android ====================
     */
    implementation AndroidLibraries.appCompat

    implementation AndroidLibraries.browser

    implementation AndroidLibraries.camera2
    implementation AndroidLibraries.cameraLifecycle
    implementation AndroidLibraries.cameraView

    implementation AndroidLibraries.core
    implementation AndroidLibraries.constraintLayout

    implementation AndroidLibraries.hiltAndroid
    kapt AndroidLibraries.hiltKapt

    implementation AndroidLibraries.material

    implementation AndroidLibraries.lifecycleLiveDataKtx
    implementation AndroidLibraries.viewModel

    implementation AndroidLibraries.navigationFrag
    implementation AndroidLibraries.navigation

    implementation AndroidLibraries.playCore
    implementation AndroidLibraries.playCoreKtx
    implementation AndroidLibraries.preference

    implementation AndroidLibraries.roomRuntime
    implementation AndroidLibraries.room
    kapt AndroidLibraries.roomCompiler

    /**
     * ==================== Firebase ====================
     */
    implementation platform(FirebaseLibraries.bom)
    implementation FirebaseLibraries.analytics
    implementation FirebaseLibraries.crashlytics
    implementation FirebaseLibraries.indexing
    implementation FirebaseLibraries.performance
    implementation FirebaseLibraries.qrCode

    /**
     * ================= 3rd Party Libs ==================
     */
    // App Intro
    implementation Libraries.appIntro


    // Coil Image Loader
    implementation Libraries.coil

    // Epoxy
    implementation Libraries.epoxy
    kapt Libraries.epoxyKapt

    // Khronos
    implementation Libraries.khronos

    // Koptional
    implementation Libraries.koptional

    // Lottie
    implementation Libraries.lottie

    // Material Value
    implementation Libraries.materialValues

    // QR Generator
    implementation Libraries.awesomeQr

    // RecyclerView Divider
    implementation Libraries.rvDivider

    // Timber
    implementation Libraries.timber

    // View Binding Delegate
    implementation Libraries.viewBindingDelegate

    debugImplementation 'com.facebook.flipper:flipper:0.83.0'
    debugImplementation 'com.facebook.soloader:soloader:0.10.1'
    debugImplementation 'com.facebook.flipper:flipper-leakcanary-plugin:0.83.0'

    /**
     * ===================== Tests =======================
     */
    androidTestImplementation TestLibraries.espresso
    testImplementation TestLibraries.junit
    androidTestImplementation TestLibraries.junitExt

    /**
     * ===================== Debug ======================
     */
    debugImplementation DebugLibraries.leakCanary

}